model{
for (pp in 1:P) {

## odds: tansition probability matrices 
## midx: regime indicators

# 1st observation
odds[pp,1,1] <- 0.9
odds[pp,1,2] <- 0.1

midx[pp,1]~dcat(odds[pp,1,])

beta0[pp] ~ dnorm(beta00, 1/pow(sd_beta0,2)) 
beta1[pp] ~ dnorm(beta10, 1/pow(sd_beta1,2))
intercept[pp,1] = beta0[pp]

Y[pp,1] ~ dnorm(50, 0.01)

AR[pp] ~ dnorm(AR0, tau_AR)T(-1,1)

gamma[1,pp] = 0
gamma[2,pp] = gamma0_2 

# from T2 to maxT[pp]
for(bb in 1:nrPeriod){

for(tt in (nrObs[pp,bb]+1):nrObs[pp,bb+1]){
      	    
# from regime midx[pp,tt-1] to regime 1
odds[pp,tt,1] <- exp(alpha[1,1,midx[pp,tt-1]]+alpha[2,1,midx[pp,tt-1]]*Period2[pp,tt]*G3[pp])

# from regime midx[pp,tt-1] to regime 2
odds[pp,tt,2] <- exp(alpha[1,2,midx[pp,tt-1]]+alpha[2,2,midx[pp,tt-1]]*Period2[pp,tt]*G3[pp])

midx[pp,tt]~dcat(odds[pp,tt,])

intercept[pp,tt] = beta0[pp] + beta1[pp]*time[bb,tt]
logIIV[pp,tt] = betaIIV00+gamma[midx[pp,tt],pp] 
Y[pp,tt] ~ dnorm(intercept[pp,tt] + AR[pp] * (Y[pp,tt-1] - intercept[pp,tt-1]), 1/exp(logIIV[pp,tt]))

} # close loop over time points
}
} # close loop over persons

# priors
# IIV
gamma0_2 = shift
shift ~ dunif(-10, 10) #Simulation: dunif(0,10); random effect in shift

AR0 ~ dnorm(0, 1)T(-1,1)
tau_AR ~ dgamma(0.001,0.001)
sd_AR = pow(tau_AR, -1/2)

beta00 ~ dnorm(50,0.01) 
beta10  ~ dnorm(0,0.01)
sd_beta0 ~ dunif(0,5)
sd_beta1 ~ dunif(0,5)

betaIIV00 ~ dnorm(0,0.01) 

for(kk in 1:2){
  alpha[kk,2,1] ~ dnorm(0,1)#Simulation: dnorm(0, 0.1)
  alpha[kk,1,1] <- 0
  alpha[kk,1,2] ~ dnorm(0,1)#Simulation: dnorm(0, 0.1)
  alpha[kk,2,2] <- 0
}

} # end of model

